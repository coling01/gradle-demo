plugins {
    id 'application'
    id 'java'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.boot' version '3.5.5'
    id 'jacoco'
}

group = 'coling'
java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
        }
    }
}

tasks.named('test') {
    useJUnitPlatform {
        excludeTags 'pact'
    }
    failFast = true
    // Mockito must be added as an agent, see:
    // https://javadoc.io/doc/org.mockito/mockito-core/latest/org.mockito/org/mockito/Mockito.html#0.3
    jvmArgs += [
            "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('mockito-core') }}", '-Xshare:off'
    ]
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
        showStandardStreams = true
    }
    reports {
        junitXml.required.set(true)
        html.required.set(true)
    }
}

tasks.named('build') {
    dependsOn tasks.named('test')
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    log4JVersion = "2.24.3"
    logbackVersion = "1.5.18"
    lombokVersion = "1.18.38"
}

application {
    mainClass = 'colin.Application'
}

dependencies {
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
    implementation 'io.swagger.core.v3:swagger-core:2.2.34'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-json'

    implementation platform('io.micrometer:micrometer-tracing-bom:latest.release')
    implementation 'io.micrometer:micrometer-tracing'
    implementation 'io.micrometer:micrometer-tracing-bridge-otel'
    implementation 'io.opentelemetry:opentelemetry-exporter-zipkin'
    implementation 'io.micrometer:micrometer-registry-azure-monitor'
    implementation 'com.azure:azure-monitor-opentelemetry-autoconfigure:1.2.0'

    implementation 'net.logstash.logback:logstash-logback-encoder:8.1'
    implementation "ch.qos.logback.contrib:logback-jackson:0.1.5"
    implementation 'org.apache.commons:commons-text:1.13.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.1'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    // testImplementation "org.springframework.boot:spring-boot-starter-test:3.5.3"
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '3.5.3', {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}